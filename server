#!/usr/bin/env perl
use strict;
use warnings;

use FindBin;
use Config;
use if "$FindBin::Bin/local/lib/perl5/$Config{version}", lib => "$FindBin::Bin/local/lib/perl5";

use DDP;
use Data::MessagePack ();
use Plack::Request;
use Plack::Runner;

my $mpack = Data::MessagePack->new->utf8;

my $maybe_apm = sub {
    my $req = shift;
    $req->method eq 'POST' && $req->path eq "/v0.4/traces" && $req->content_type =~ m{application/msgpack}i;
};

my $app = sub {
    my $env = shift;
    my $req = Plack::Request->new($env);

    if ($maybe_apm->($req)) {
        my $content = $req->content;
        my $apm = $mpack->unpack($content);
        p $apm;
        return [ 200, [], ["ok\n"] ];
    } else {
        p $env;
        return [ 400, [], ["Bad Request\n"] ];
    }
};

my $runner = Plack::Runner->new;
$runner->parse_options("--port", 8126, "--server", "Starman", @ARGV);
$runner->run($app);
